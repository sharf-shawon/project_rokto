export COMPOSE_FILE := "docker-compose.local.yml"

## Just does not yet manage signals for subprocesses reliably, which can lead to unexpected behavior.
## Exercise caution before expanding its usage in production environments.
## For more information, see https://github.com/casey/just/issues/2473 .


# Default command to list all available commands.
default:
    @just --list

# build: Build python image.
build *args:
    @echo "Building python image..."
    @docker compose build {{args}}
    @docker compose run --rm django uv lock


# up: Start up containers.
up:
    @echo "Starting up containers..."
    @docker compose up -d --remove-orphans

# down: Stop containers.
down:
    @echo "Stopping containers..."
    @docker compose down

# prune: Remove containers and their volumes.
prune *args:
    @echo "Killing containers and removing volumes..."
    @docker compose down -v {{args}}

# logs: View container logs
logs *args:
    @docker compose logs -f {{args}}

# manage: Executes `manage.py` command.
manage +args:
    @docker compose run --rm django python ./manage.py {{args}}

# shell: Run django shell
shell:
    @docker compose run --rm django python ./manage.py shell_plus

# migrate: Run migrations
migrate:
    @docker compose run --rm django python ./manage.py migrate

# test: Run pytest
test *args:
    @docker compose run --rm django pytest {{args}}

# test-coverage: Run pytest with coverage
test-coverage:
    @docker compose run --rm django coverage run -m pytest
    @docker compose run --rm django coverage report

# lint: Run pre-commit on all files
lint:
    @pre-commit run --all-files

# check: Run all quality checks (lint, type, test, coverage)
check: lint
    @docker compose run --rm django mypy project_rokto
    @just test-coverage
